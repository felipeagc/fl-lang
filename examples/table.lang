import "core:c";

const DEFAULT_TABLE_SIZE: u64 = 32;

func hash_str(str: [_]char) -> u64 {
    var hash: u64 = 5381;

    for (var i: u64 = 0; i < str.len; i += 1) {
        hash = ((hash << 5) + hash) + cast(u64)str[i];
    }

    return hash;
}

#[inline]
func str_equal(a: [_]char, b: [_]char) -> bool {
    if (a.len != b.len) return false;
    for (var i: uint = 0; i < a.len; i += 1) {
        if (a[i] != b[i]) return false;
    }
    return true;
}

pub typedef Table(V) struct {
    size: u64,
    keys: *[_]char,
    hashes: *u64,
    values: *V,
};

pub func table_init(V)(table: *Table(V), size: u64) {
    table.size = size;
    if (table.size == 0) {
        table.size = DEFAULT_TABLE_SIZE;
    }

    // Round up to nearnest power of two
    table.size -= 1;
    table.size |= table.size >> 1;
    table.size |= table.size >> 2;
    table.size |= table.size >> 4;
    table.size |= table.size >> 8;
    table.size |= table.size >> 16;
    table.size |= table.size >> 32;
    table.size += 1;

    // Init memory
    table.hashes = calloc(table.size, @size_of(table.hashes[0]));
    table.values = calloc(table.size, @size_of(table.values[0]));
    table.keys = calloc(table.size, @size_of(table.keys[0]));
}

pub func table_deinit(V)(table: *Table(V)) {
    free(table.keys);
    free(table.hashes);
    free(table.values);
}

pub func table_set(V)(table: *Table(V), key: [_]char, value: V) -> V {
    var hash: u64 = hash_str(key);
    var i: u64 = hash & (table.size - 1);
    var iters: u64 = 0;

    while ((table.hashes[i] != hash || !str_equal(table.keys[i], key))
            && table.hashes[i] != 0 && iters < table.size)
    {
        i = (i + 1) & (table.size - 1);
        iters += 1;
    }

    if (iters >= table.size) {
        table_grow(V)(table);
        return table_set(V)(table, key, value);
    }

    table.keys[i] = key;
    table.hashes[i] = hash;
    table.values[i] = value;

    return value;
}

pub func table_get(V)(table: *Table(V), key: [_]char, result: *V) -> bool {
    var hash: u64 = hash_str(key);
    var i: u64 = hash & (table.size - 1);
    var iters: u64 = 0;

    while ((table.hashes[i] != hash || !str_equal(table.keys[i], key))
            && table.hashes[i] != 0 && iters < table.size)
    {
        i = (i + 1) & (table.size - 1);
        iters += 1;
    }

    if (iters >= table.size) {
        return false;
    }

    if (table.hashes[i] != 0) {
        if (result) *result = table.values[i];
        return true;
    }

    return false;
}

func table_grow(V)(table: *Table(V)) {
    var old_size = table.size;
    var old_keys = table.keys;
    var old_hashes = table.hashes;
    var old_values = table.values;

    table.size *= 2;
    table.hashes = calloc(table.size, @size_of(table.hashes[0]));
    table.values = calloc(table.size, @size_of(table.values[0]));
    table.keys = calloc(table.size, @size_of(table.keys[0]));

    for (var i: u64 = 0; i < old_size; i += 1) {
        if (old_hashes[i] != 0) {
            table_set(V)(table, old_keys[i], old_values[i]);
        }
    }

    free(old_keys);
    free(old_hashes);
    free(old_values);
}

func main() {
    {
        var table: Table(int);
        table_init(int)(&table, 5);
        defer table_deinit(int)(&table);

        table_set(int)(&table, "hello", 5);

        {
            var result: int;
            if (!table_get(int)(&table, "hello", &result)) abort();
            if (result != 5) abort();
            printf(c"table[\"hello\"] = %ld\n", result);
        }
    }

    {
        var table: Table(u16);
        table_init(u16)(&table, 5);
        defer table_deinit(u16)(&table);

        table_set(u16)(&table, "hello", 5);

        {
            var result: u16;
            if (!table_get(u16)(&table, "hello", &result)) abort();
            if (result != 5) abort();
            printf(c"table[\"hello\"] = %hu\n", result);
        }
    }
}
